
🔮 0 1

👴 A 🔒 represents a key-value tuple stored within a 📔.
🐇 🔒 🐚 V 🔵 🍇
	🍰 k 🔡
	🍰 v V

	👴 Construct a 🔒 from a key-value pair.
	🐈 🆕 k. 🔡 v. V 🍇
		🍮 k k.
		🍮 v v.
	🍉

	👴 Key getter
	🐖 🔑 ➡ 🔡 🍇
		🍎 k
	🍉

	👴 Value getter
	🐖 🔓 ➡ V 🍇
		🍎 v
	🍉

	👴 Value setter
	🐖 🔏 v. V 🍇
		🍮 v v.
	🍉
🍉

👴 A 📔 is a fixed-size hashmap that associates string keys (🔡) with object values (🔵).
🌍 🐇 📔 🐚 V 🔵 🍇
	🍰 size 🚂
	🍰 capacity 🚂
	👴 List of linked lists of optional generic 🔒s
	🍰 objects 🍨🐚🍨🐚🍬🔒🐚V

	👴 Constructs a new fixed-size 📔 of the given size.
	🐈 📦 capacity. 🚂 🍇
		🍮 size 0
		🍮 capacity capacity.
		🍮 objects 🔷 🍨🐚🍨🐚🍬🔒🐚V 🐧 capacity
	🍉

	👴 Maps the given key to the given value. Returns true on success.
	🐖 📝 k 🔡 v V ➡️ 👌 🍇
		🍦 i 🔑🐕 k

		🍊🍦 list 🐽 objects i 🍇
			🍮 found 👎
			🍮 j 0
			🍦 n 🐔 list
			🔁 🎊 ❎ found ◀ j n 🍇
				🍦 curr 🍺 🐽 list j
				🍦 k' 🔑 curr
				🍊 😛 k' k 🍇
					🍮 found 👍
				🍉 🍓 🍇
					🍫 j
				🍉
			🍉
			🍊 ◀ j n 🍇
				🍦 curr 🍺 🐽 list j
				🔏 curr v
			🍉 🍓 🍇
				🍊 😛 size capacity 🍇
					🍎 👎
				🍉
				🍦 new 🔷 🔒🐚V 🆕 k v
				🐻 list new
				🍫 size
			🍉
		🍉 🍓 🍇
			🍦 l 🔷 🍨🐚🔒🐚V 🐸
			🍦 v' 🔷 🔒🐚V 🆕 k v
			🐻 l v'
			🐷 objects i l
			🍫 size
		🍉
		🍎 👍
	🍉

	👴 Returns the value associated with the given key as an optional.
	🐖 📖 k 🔡 ➡️ 🍬V 🍇
		🍦 i 🔑🐕 k
		🍦 list 🐽 objects i
		🍊🍦 list' list 🍇
			🔂 v list' 🍇
				🍊 😛 🔑 🍺 v k 🍇
					🍎 🔓 🍺 v
				🍉
			🍉
		🍉
		🍎 ⚡️
	🍉

	👴 Deletes and returns the value associated with the given key.
	🐖 🗑 k 🔡 ➡️ 🍬V 🍇
		🍊 ▶ size 0 🍇
			🍳 size
			🍦 i 🔑🐕 k
			🍊🍦 list 🐽 objects i 🍇
				🍮 j 0
				🍦 n 🐔 list
				🔁 ◀ j n 🍇
					🍦 elem 🍺 🐽 list j
					🍊 😛 🔑 elem k 🍇
						🐨 list j
						🍎 🔓 elem
					🍉
					🍫 j
				🍉
			🍉
		🍉
		🍎 ⚡️
	🍉

	👴 Returns the load factor of this 📔.
	👴 float load()
	🐖 📈 ➡️ 🚀 🍇
		🍎 ➗ 🚀 size 🚀 capacity
	🍉

	👴 Hashes a string key into an array index.
	👴 Uses the djb2 hash algorithm (see: http://www.cse.yorku.ca/~oz/hash.html)
	🔐 🐖 🔑️ s 🔡 ➡️ 🚂 🍇
		🍮 hash 5381
		🔂 c 🍡 s 🍇
			🍦 n 🚂 c
			🍮 hash ➕ ➕ 👈 hash 5 hash n
		🍉
		🍎 🚮 hash capacity
	🍉
🍉
