📦 emojitable 🔴
📜 🔤../include/testsHelper.emojic🔤

🏁 ➡️ 🚂 🍇
	🍦 tester 🔷💯🆕
	🏁 tester
	🍎 👔 tester
🍉

🐇 💯 👈 🍇
	✒️ 🐖 🏁 🍇
		😀 🔤Running tests on emojitable...🔤

		👴 Create a new emojitable with capacity 10
		🍦 table 🔷 📔🐚🔡 📦 10

		👴 Test that individual elements can be set/get.
		⛔️🐕 📝 table 🔤Hello🔤 🔤World🔤 🔤insert succeeds ("Hello" -> "World")🔤
		🍦 value 📖 table 🔤Hello🔤
		⛔️🐕 ❎ ☁ value 🔤Read value ("World") is non-nothing🔤
		⛔️🐕 😛 🍺 value 🔤World🔤 🔤Read value is "World"🔤
		⛔️🐕 😛 📈 table 0.1 🔤Load factor is correct🔤

		👴 Test that multiple elements can be get/set.
		⛔️🐕 📝 table 🔤Colin🔤 🔤King🔤 🔤Insert succeeds ("Colin" -> "King")🔤
		⛔️🐕 📝 table 🔤John🔤 🔤Dough🔤 🔤Insert succeeds ("John" -> "Dough")🔤
		⛔️🐕 📝 table 🔤Prince🔤 🔤Charming🔤 🔤Insert succeeds ("Prince" -> "Charming")🔤
		⛔️🐕 😛 📈 table 0.4 🔤Updated load factor is correct🔤

		👴 Verify that deleted elements are deleted.
		🍦 value2 🗑 table 🔤Prince🔤
		⛔️🐕 ❎ ☁ value2 🔤Deleted value ("Prince" -> "Charming") is non-nothing🔤
		⛔️🐕 😛 🍺 value2 🔤Charming🔤 🔤Read value is "Charming"🔤
		⛔️🐕 😛 📈 table 0.3 🔤Updated load factor is correct after deletion🔤

		👴 Verify that getting a nonexistant or deleted element is nothing.
		🍦 value3 📖 table 🔤Prince🔤
		⛔️🐕 ☁ value3 🔤Reading deleted value returns nothing🔤
		🍦 value4 📖 table 🔤Princess🔤
		⛔️🐕 ☁ value4 🔤Reading non-existant value returns nothing🔤

		👴 Verify that deleted elements can be re-inserted.
		⛔️🐕 📝 table 🔤Prince🔤 🔤Nelson🔤 🔤Re-inserting deleted element succeeds ("Prince" -> "Nelson")🔤
		🍦 value5 📖 table 🔤Prince🔤
		⛔️🐕 ❎ ☁ value5 🔤Reading re-inserted value ("Nelson") returns non-nothing🔤
		⛔️🐕 😛 🍺 value5 🔤Nelson🔤 🔤Returned value is "Nelson"🔤
		⛔️🐕 😛 📈 table 0.4 🔤Updated load factor is correct🔤

		👴 Verify that overwriting a key works correctly
		⛔️🐕 📝 table 🔤John🔤 🔤Bender🔤 🔤Overwriting a key succeeds ("John" -> "Bender")🔤
		🍦 value6 📖 table 🔤John🔤
		⛔️🐕 ❎ ☁ value6 🔤Reading overwritten value ("Bender") returns non-nothing🔤
		⛔️🐕 😛 🍺 value6 🔤Bender🔤 🔤Returned value is "Bender"🔤
		⛔️🐕 😛 📈 table 0.4 🔤Load factor does not change🔤
		👴 Verify that removing the overwritten key completely removes the key
		🍦 value7 🗑 table 🔤John🔤
		⛔️🐕 ❎ ☁ value7 🔤Deleted value ("John" -> "Bender") is non-nothing🔤
		⛔️🐕 😛 🍺 value7 🔤Bender🔤 🔤Read value is "Bender"🔤
		⛔️🐕 😛 📈 table 0.3 🔤Updated load factor is correct after deletion🔤
		🍦 value8 📖 table 🔤John🔤
		⛔️🐕 ☁ value8 🔤Deleted value ("John" -> "Bender") is removed🔤

		👴 Verify that deleting more elements than exist fails
		🗑 table 🔤Hello🔤
		🗑 table 🔤Colin🔤
		🗑 table 🔤Prince🔤
		⛔️🐕 😛 📈 table 0 🔤Cleared table returns 0 load factor🔤
		🍦 value9 🗑 table 🔤Princess🔤
		⛔️🐕 ☁ value9 🔤Too many deletion operations returns nothing🔤

		👴 Verify that inserting more elements than capacity fails
		🍮 i 0
		🔁 ◀ i 10 🍇
			📝 table 🍪 🔤key🔤 🔡 i 10 🍪 🍪 🔤value🔤 🔡 i 10 🍪
			🍫 i
		🍉
		⛔️🐕 😛 📈 table 1.0 🔤A full table has a 1.0 load factor🔤
		⛔️🐕 ❎ 📝 table 🔤key11🔤 🔤value11🔤 🔤Inserting too many times fails🔤

		⛔️🐕 📝 table 🔤key5🔤 🔤value5🔤 🔤Inserting a pre-existing key in a full table succeeds🔤
	🍉
🍉
